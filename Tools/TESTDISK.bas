100 'CREATE DISK TEST
110 '
120 CLEAR 1000
130 CLS
140 PRINT "CREATE TEST DISK"
150 PRINT "================"
160 PRINT
170 INPUT "DISK DRIVE (1-4)";DD
180 IF DD<>1 AND DD<>2 AND DD<>3 AND DD<>4 GOTO 590
190 SREAD DD,20,1,A$,B$
200 TT=ASC(MID$(B$,125,1))
210 SS=ASC(MID$(B$,126,1))
220 PRINT "TRACKS        : ";TT
230 PRINT "TRACK SECTORS : ";SS
240 SN=TT*SS
250 PRINT "TOTAL SECTORS : ";SN
260 F=FREE(DD)
270 IF TT=40 AND SS=18 AND F<>175104 GOTO 610
280 IF TT=40 AND SS=36 AND F<>359424 GOTO 610
290 IF TT=80 AND SS=18 AND F<>359424 GOTO 610
300 IF TT=80 AND SS=36 AND F<>728064 GOTO 610
310 SP$=CHR$(129)+CHR$(130)+CHR$(136)+CHR$(132)
320 Z$=""
330 FOR I=1 TO 128
340 Z$=Z$+CHR$(0)
350 NEXT I
360 FOR I=0 TO SN-1
370 GOSUB 420
380 GOSUB 460
390 NEXT I
400 GOTO 550
410 'PROGRESS STATUS
420 PRINT @257, USING "### %";I*100/SN
430 PRINT @31,MID$(SP$,I-INT(I/4)*4+1,1);
440 RETURN
450 'WRITE SECTOR DATA
451 'I=LOGICAL SECTOR NUMBER
460 S1$=Z$
470 S2$=Z$
471 T=INT(I/SS)
472 S=I-T*SS
480 MID$(S1$,1,1)=CHR$(T)
490 MID$(S1$,2,1)=CHR$(INT(S/18))
491 MID$(S1$,3,1)=CHR$(S-INT(S/18)*18+1)
520 PRINT @480,USING "### ###";T,S+1;
530 SWRITE DD,T,S+1,S1$,S2$
540 RETURN
550 PRINT
560 PRINT "DONE!"
570 END
580 'TERMINATE WITH ERROR
590 PRINT "ERROR: INVALID DISK DRIVE"
600 END
610 PRINT "ERROR: DISK IS NOT EMPTY"
620 END
630 PRINT "ERROR: CANNOT WRITE DISK SECTOR"
640 END
